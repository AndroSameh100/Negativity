plugins {
    id 'com.github.johnrengelman.shadow'
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
}

repositories {
    jcenter()
    maven {
        url 'https://repo-new.spongepowered.org/repository/maven-snapshots/'
    }
    maven {
        url 'https://repo-new.spongepowered.org/repository/sponge-legacy/'
    }
}

dependencies {
    api project(':common')
    compileOnly "org.spongepowered:spongeapi:$spongeApiVersion"
}

processResources {
    expand(project.properties)
}

shadowJar {
    archiveBaseName.set('negativity')
    archiveAppendix.set('sponge8')
    archiveClassifier.set(null)
}

build.dependsOn shadowJar

task prepareServer(type: Copy) {
    from shadowJar.outputs
    rename { 'negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}
