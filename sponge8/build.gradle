import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.PluginDependency

plugins {
    id 'com.github.johnrengelman.shadow'
    id 'org.spongepowered.gradle.plugin' version '1.1.1'
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
}

dependencies {
    api project(':common')
}

sponge {
    apiVersion(spongeApiVersion.replace('-SNAPSHOT', ''))
    plugin('negativity') {
        loader(PluginLoaders.JAVA_PLAIN)
        displayName('Negativity')
        mainClass('com.elikill58.negativity.sponge8.SpongeNegativity')
        description('An Advanced AntiCheat Plugin')
        links {
            homepage('https://ore.spongepowered.org/Elikill58/Negativity')
            source('https://github.com/Elikill58/Negativity')
            issues('https://github.com/Elikill58/Negativity/issues')
        }
        contributor('Elikill58') {
            description('Author')
        }
        contributor('RedNesto') {
            description('Author')
        }
        dependency('spongeapi') {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
    }
}

shadowJar {
    archiveBaseName.set('negativity')
    archiveAppendix.set('sponge8')
    archiveClassifier.set(null)

    mergeServiceFiles()
    from(project(':common').tasks.shadowJar)
    from(writePluginMetadata)
}

build.dependsOn shadowJar

task prepareServer(type: Copy) {
    from shadowJar.outputs
    rename { 'negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}

runServer {
    def runDir = System.getProperty("sponge8.run.dir")
    if (runDir != null) {
        workingDir(runDir)
    }
}
